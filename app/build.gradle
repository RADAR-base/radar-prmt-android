apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 32
    buildToolsVersion '32.0.0'

    defaultConfig {
        applicationId 'org.radarcns.detail'
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 57
        versionName '1.0.18-SNAPSHOT'
        manifestPlaceholders = ['appAuthRedirectScheme': 'org.radarbase.passive.app']
        multiDexEnabled true
        ndkVersion "23.1.7779620"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel = 'full'
            }
        }
        debug {
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_11
        sourceCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
    }
    flavorDimensions "channel"
    productFlavors {
        playStore {
            dimension "channel"
            versionNameSuffix "-playStore"
        }
        selfRelease {
            dimension "channel"
            versionNameSuffix "-selfRelease"
        }
    }
    lint {
        abortOnError false
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    flatDir { dirs 'libs' }
    google()
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://repo.thehyve.nl/content/repositories/releases' }
}

ext.radarCommonsVersion = '1.2.0-SNAPSHOT'

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api "org.radarbase:radar-commons-android:$radarCommonsVersion"

    implementation "org.radarbase:radar-android-login-qr:$radarCommonsVersion"

    implementation platform('com.google.firebase:firebase-bom:30.2.0')
    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'org.slf4j:slf4j-api:2.0.0'

    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'

    implementation 'com.gitlab.mvysny.slf4j:slf4j-handroid:2.0.0'

    implementation "org.radarbase:radar-android-phone:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-phone-usage:$radarCommonsVersion"
    selfReleaseImplementation "org.radarbase:radar-android-phone-telephony:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-empatica:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-application-status:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-weather:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-audio:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-faros:$radarCommonsVersion"
    implementation "org.radarbase:radar-android-ppg:$radarCommonsVersion"

    implementation 'androidx.lifecycle:lifecycle-process:2.5.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    testImplementation 'junit:junit:4.13.2'
    testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.0'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = '11'
        apiVersion = '1.7'
        languageVersion = '1.7'
    }
}

// Needed for Firebase. Put at the bottom so it can detect the Firebase version.
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
