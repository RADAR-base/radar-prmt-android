apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.gms.oss.licenses.plugin'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId 'org.radarcns.detail'
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 22
        versionName '0.3.6-SNAPSHOT'
        manifestPlaceholders = ['appAuthRedirectScheme': 'org.radarcns.detail']
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'

    // fixes https://github.com/facebook/flipper/issues/146
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "com.android.support") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "26.+"
            }
        }
    }
}

repositories {
    flatDir { dirs 'libs' }
    google()
    jcenter()
    maven { url 'http://dl.bintray.com/radar-cns/org.radarcns' }
    maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
    maven { url  'https://repo.thehyve.nl/content/repositories/releases' }
}

dependencies {
    api 'org.radarcns:radar-commons-android:0.8.2'

    implementation 'org.radarcns:radar-android-login-qr:0.8.2'
    implementation 'com.google.firebase:firebase-core:16.0.4'
    implementation 'com.google.firebase:firebase-config:16.1.0'
    implementation 'com.google.android.gms:play-services-oss-licenses:16.0.1'

    implementation 'org.slf4j:slf4j-api:1.7.25'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'

    runtimeOnly 'org.radarcns:radar-android-phone:0.1.8'
    //runtimeOnly 'org.radarcns:radar-android-empatica:0.1.1'
    runtimeOnly 'org.radarcns:radar-android-application-status:0.3.2'
    runtimeOnly 'org.radarcns:radar-android-weather:0.2.0'
    runtimeOnly 'org.radarcns:radar-android-audio:0.1-alpha.2'
    runtimeOnly 'org.radarcns:radar-android-faros:0.1.1'
    implementation 'org.radarcns:radar-android-ppg:0.1.1'

    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    testImplementation 'junit:junit:4.12'
    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.25'
}

// Needed for Firebase. Put at the bottom so it can detect the Firebase version.
apply plugin: 'com.google.gms.google-services'
